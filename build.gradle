buildscript {

    repositories {

        jcenter()

        maven {

            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {

            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {

        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
    }
}

apply plugin: "maven"

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "org.sonarqube"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "net.darkhax.darkutils"
archivesBaseName = "DarkUtilities"
version = '1.1.8.finalrc1'

//Defines the MC environment information.
minecraft {

    version = "${version_forge}"
    runDir = "run"
    mappings = "${version_mcp}"
    useDepAts = true
}

//Defines basic patterns for pulling various dependencies.
repositories {

    maven {

        url "http://dvs1.progwml6.com/files/maven"
    }

    maven {

        url "https://maven.mcmoddev.com"
    }
    
    maven {
        
        url "http://tehnut.info/maven"
    }
}

dependencies {

    deobfCompile "mcp.mobius.waila:Hwyla:${version_hwyla}"
    deobfCompile "mezz.jei:jei_1.10.2:${version_jei}"
    deobfCompile "net.darkhax.bookshelf:Bookshelf:${version_bookshelf}"
    deobfCompile "com.azanor:Baubles:${version_baubles}"
}

//Handles basic resources such as mcmod.info
processResources {

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {

        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {

        exclude 'mcmod.info'
    }
}

//Pulls the build version from a Java file.
String getVersionFromJava(File file) {

    String release = "0";
    String update = "0";
    String patch = "0";

    String build = System.getenv("BUILD_NUMBER") ? System.getenv("BUILD_NUMBER") : "0";
    def outfile = "";
    def ln = System.getProperty("line.separator")

    String prefix = "public static final String VERSION_NUMBER = \"";
    file.eachLine {
        String s ->

            String v = s.trim();

        if (v.startsWith(prefix)) {

            v = v.substring(prefix.length(), v.length() - 2);
            String[] pts = v.split("\\.");

            release = pts[0];
            update = pts[1];
            patch = pts[2];
            s = s.replaceAll(".0\";", ".${build}\";");
        }

        outfile += (s + ln);
    }

    file.write(outfile);

    return "1.10.2-$release.$update.$patch.$build";
}

//Creates the source jar.
task sourcesJar(type: Jar, dependsOn: classes) {

    from sourceSets.main.allSource
    classifier = 'sources'
}

//Creates the JavaDoc jar.
task javadocJar(type: Jar, dependsOn: javadoc) {

    from javadoc.destinationDir
    classifier = 'javadoc'
}

//Creates the deobfuscated jar.
task deobfJar(type: Jar) {

    from sourceSets.main.output
    classifier = "deobf"
}

//Adds the deobf, source and javadoc jars to the build artifacts.
artifacts {

    archives deobfJar
    archives sourcesJar
    archives javadocJar
}

//Uploads the artifacts to the maven server.
uploadArchives {

    repositories {

        mavenDeployer {

            repository(url: "file:///var/www/html/maven")
        }
    }
}

//Prevents incomplete JavaDocs from preventing a build on Java 8.
if (JavaVersion.current().isJava8Compatible()) {

    allprojects {

        tasks.withType(Javadoc) {

            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

//Provides the basic CurseForge upload details
curseforge {

    //Sets the API from the system environment, if available.
    apiKey = System.getenv("curseForgeApiKey") ? System.getenv("curseForgeApiKey") : "0";

    project {

        //Sets the Project ID from the system environment, if available.
        id = System.getenv("projectId") ? System.getenv("projectId") : "0";
        
        releaseType = "alpha";
        changelog = "All changes can be found here: https://github.com/Darkhax-Minecraft/Dark-Utilities/commits/master/";

        addGameVersion "1.10.2"
        
        //Adds main jar, deobf jar, source jar, and javadoc jar to the upload.
        mainArtifact(jar) {
          
            relations {

                //Adds the file relations to the upload.
                requiredLibrary 'bookshelf'
                optionalLibrary 'waila'
                optionalLibrary 'baubles'
            }
        }
        
        addArtifact(deobfJar)
        addArtifact(sourcesJar)
        addArtifact(javadocJar)
    }
}

sonarqube {

    properties {
        property "sonar.host.url", "https://sonarqube.com"
        property "sonar.login", System.getenv("sonarQubeApiKey")
    }
}