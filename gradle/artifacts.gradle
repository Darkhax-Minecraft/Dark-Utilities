/*
 This gradle script provides some basic tasks for creating common artifact types that Minecraft 
 developers like to distribute. 
*/

task sourcesJar(type: Jar, dependsOn: classes) {

    description = 'Creates a JAR containing the source code.'
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    description = 'Creates a JAR containing the JavaDocs.'
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task deobfJar(type: Jar) {

    description = 'Creates a JAR containing the non-obfuscated compiled code.'
    from sourceSets.main.output
    classifier = "deobf"
}

task forgelibJar(type: Jar) {
    description = 'Creates a compiled JAR which also contains raw sources.'
    from sourceSets.main.output
    from sourceSets.main.allJava
    classifier = 'forgelib'
}

//Adds the artifact types added by this script to the actual artifacts list. 
artifacts {

    archives sourcesJar
    archives javadocJar
    archives deobfJar
    archives forgelibJar
}
